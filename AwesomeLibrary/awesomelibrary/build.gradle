apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def artifactName = 'awesomelibrary'
def packageName = 'com.jeroenmols.awesomelibrary'
def libraryVersion = '1.0.0'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "ar.com.synergian:wagon-git:0.2.0"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId packageName
            version = libraryVersion
            artifactId artifactName

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/$artifactName-release.aar")

            pom.withXml {
                def pluginRepositories = asNode().appendNode('pluginRepositories')
                def pluginRepository = pluginRepositories.appendNode('pluginRepository')
                pluginRepository.appendNode('id', 'synergian-repo')
                pluginRepository.appendNode('url', 'https://raw.github.com/synergian/wagon-git/releases')

                def build = asNode().appendNode('build')
                def extensions = build.appendNode('extensions')
                def extension = extensions.appendNode('extension')
                extension.appendNode('groupId', 'ar.com.synergian')
                extension.appendNode('artifactId','wagon-git')
                extension.appendNode('version','0.2.5-git')

            }
        }
    }

    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url "git:releases://git@bitbucket.org:jmols/android_test_wagon-git.git"
        }

    }
}

//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: "repo url") {
//                authentication(userName: yourbitbucketusername,
//                        password: yourbitbucketpassword)
//            }

artifactory {
    contextUrl = 'http://localhost:8081/artifactory'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = 'libs-release-local'

            username = "admin"
            password = "password"
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

